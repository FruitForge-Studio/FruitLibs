plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.github.revxrsal.zapper' version '1.0.3'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'com.fruitforge'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT"

    implementation "io.github.revxrsal:zapper.api:1.0.3"
    implementation "net.kyori:adventure-text-minimessage:4.18.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.18.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"
}

zapper {
    libsFolder = "libraries"
    relocationPrefix = "com.fruitforge.fruitlibs.libs"

    repositories {
        maven("https://repo.maven.apache.org/maven2/")
        maven("https://oss.sonatype.org/content/groups/public/")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    archiveFileName.set("${project.name}-${project.version}.jar")

    minimize {
        exclude(dependency('.*:.*:.*'))
    }

    relocate 'net.kyori', 'com.fruitforge.fruitlibs.shaded.kyori'
}

build {
    dependsOn shadowJar
}

jar {
    enabled = false
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}